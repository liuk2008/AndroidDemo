apply plugin: 'com.android.application'
apply plugin: 'com.android.plugin.viewinject'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId "com.android.demo"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        // dex突破65535的限制
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file("../signature/debug.jks")
            storePassword "123456"
            keyAlias "debug"
            keyPassword "123456"
//            v2SigningEnabled false     // 禁止7.0 以上版本进行V2签名校验，兼容python多渠道打包
        }

        release {
            storeFile file("../signature/release.jks")
            storePassword "123456"
            keyAlias "product"
            keyPassword "123456"
        }
    }

    buildTypes {
        release {
            minifyEnabled true                                          // 是否进行混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources true                                         // 移除无用的resource文件
            zipAlignEnabled true                                        // Zipalign优化
            debuggable false                                              // 开启调试
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            debuggable true
            signingConfig signingConfigs.debug
//            applicationIdSuffix ".debug"               //配置不同的applicationId
        }
        qa {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            debuggable true
            signingConfig signingConfigs.debug
            matchingFallbacks = ['debug']
        }
    }

    // 移除lint检查的error
    lintOptions {
        abortOnError false
        disable 'GoogleAppIndexingWarning'
    }

    // 配置打包名称
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFileName = output.outputFileName
            if (outputFileName != null && outputFileName.endsWith('.apk')) {
                def fileName = "AndroidDemo_v${defaultConfig.versionName}_${rootProject.ext.date}_${variant.name}.apk"
                output.outputFileName = new File(fileName)
            }
        }
    }

}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation project(":lib:lib-base")
    implementation project(":lib:lib-geetest")
    implementation project(":module:account")

    // 使用自定义View注解
    implementation 'com.android.library:viewinject:1.0'
    annotationProcessor 'com.android.library:viewinject-compiler:1.0'

}


